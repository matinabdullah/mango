import java.util.List
import io.pelle.mango.client.base.modules.hierarchical.HierarchicalConfigurationVO
import java.util.Map
import io.pelle.mango.client.base.vo.IBaseVO
import io.pelle.mango.client.base.messages.IValidationMessage
import io.pelle.mango.client.base.db.vos.Result
import io.pelle.mango.client.base.vo.query.SelectQuery
 
project Mango {

	package io.pelle.^mango {

		valueobject SearchResultItem {
		}
		
		valueobject Systeminformation {
			string hostName
			string ipAddress
		}
		
		valueobject LogEntry {
			string logText
		}
		
		entity DictionaryLabelSearchIndex {
			string dictionaryName
			string voClassName
			long voId
			string ^text
		}

		entity DictionaryHierarchicalNode {
			string ^label
			string dictionaryName
			long voId
			string voClassName
			string parentClassName
			long parentVOId
			boolean hasChildren
			entity DictionaryHierarchicalNode 0..n children
		}
				
		stringdatatype FileName {
		}
		
		binarydatatype FileContent {
		}

		stringdatatype FileUUID {
		}

		entity File  {
			string FileName fileName
			binary FileContent fileContent
			string FileUUID fileUUID
		}
				
		service HierachicalService {

			servicemethod List<DictionaryHierarchicalNode> getRootNodes ( String id )
			
			servicemethod HierarchicalConfigurationVO getConfigurationById  ( String id )
			
			servicemethod List<HierarchicalConfigurationVO> getConfigurations ()
			
			servicemethod Boolean hasChildren ( String voClassName, Long voId )
			
			servicemethod List<DictionaryHierarchicalNode> getChildNodes ( String id, Long voId, String voClassName )
		
		}
			
		service SystemService {

			servicemethod Systeminformation getSystemInformation ()
	
			servicemethod LogEntry getLog ()

		}
			
		
		package modules {

			moduledefinition ModuleNavigation {
				parameters {
					parameter NavigationTreeElementName {
						type string
					} 
				}
			}

			moduledefinition ModuleHierarchicalTree {
				parameters {
					parameter HierarchicalTreeId {
						type string
					} 
					parameter ShowAddNodes {
						type boolean
					} 
				}
			}

		
			moduledefinition DictionarySearch {
			
				parameters {
				
					parameter SearchDictionaryName {
						type string
					}
					 
					parameter SearchText {
						type string
					} 
				}
			}

			moduledefinition DictionaryQuery {
			}

			moduledefinition DictionaryEditor {
			
				parameters {
				
					parameter EditorDictionaryName {
						type string
					} 
					
					parameter Id {
						type long
					} 
					
				}
			}
		}
		
		service BaseEntityService {

			servicemethod <NewVOType extends IBaseVO> NewVOType getNewVO ( String voClassName, Map<String,String> properties )

			servicemethod <CreateVOType extends IBaseVO> CreateVOType create ( CreateVOType vo )

			servicemethod <ValidateVOType extends IBaseVO> List<IValidationMessage> validate ( ValidateVOType vo )

			servicemethod <ValidateAndSaveVOType extends IBaseVO> Result<ValidateAndSaveVOType> validateAndSave ( ValidateAndSaveVOType vo )

			servicemethod <ValidateAndCreateVOType extends IBaseVO> Result<ValidateAndCreateVOType> validateAndCreate ( ValidateAndCreateVOType vo )
		
			servicemethod <SaveVOType extends IBaseVO> SaveVOType save ( SaveVOType vo )
				
			servicemethod <FilterVOType extends IBaseVO> List<FilterVOType> filter ( SelectQuery<FilterVOType> selectQuery )

			servicemethod void deleteAll ( String voClassName )

			servicemethod <DeleteVOType extends IBaseVO> void delete ( DeleteVOType vo )

			servicemethod <ReadVOType extends IBaseVO> ReadVOType read ( Long id, String voClassName )
		}
	}
}