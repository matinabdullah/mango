apply plugin: 'xtext'

xtext {
	version = '2.5.3' // the current default, can be omitted
	encoding = 'UTF-8' //the default, can be omitted

	languages{
		
		mango {
			setup = 'io.pelle.mango.dsl.MangoStandaloneSetup'
			consumesJava = true
		}
	}
}

configurations {

	generatedEntities
	generatedVOs
	generatedXml
	
	compile.extendsFrom(generatedVOs)
	compile.extendsFrom(generatedXml)
	compile.extendsFrom(generatedEntities)
	
	archives.extendsFrom(generatedEntities)
	archives.extendsFrom(generatedEntitiesSources)
	archives.extendsFrom(generatedVOs)
	archives.extendsFrom(generatedVOsSources)
	archives.extendsFrom(generatedXml)
	archives.extendsFrom(generatedXmlSources)
}


dependencies {
	
	xtextTooling project(':mango-dsl')
	xtextTooling project(':mango-db')
	xtextTooling project(':mango-server-base')
	
	compile project(':mango-db')
	compile project(':mango-server-base')
	project(':mango-dsl')
	
	testCompile project(path: ':mango-db', configuration: 'testArtifacts')
	
	// webservices
	generatedEntities group: "org.springframework.ws", name: "spring-xml", version: "2.2.0.RELEASE"
	generatedEntities group: "org.springframework.ws", name: "spring-ws-core", version: "2.2.0.RELEASE"
	generatedEntities group: "wsdl4j", name: "wsdl4j", version: "1.6.3"
	generatedEntities group: "org.apache.ws.xmlschema", name: "xmlschema-core", version: "2.1.0"

}

sourceSets {
	
	generatedVOs {
		java {
			srcDirs = [ 'src-gen-vos' ]
		}
		resources {
			srcDirs = [ 'src-gen-vos' ]
		}
		
		compileClasspath += project.sourceSets.main.compileClasspath
	}

	generatedEntities {
		
		java {
			srcDirs = [ 'src-gen-entities' ]
		}
		resources {
			srcDirs = [ 'src-gen-entities' ]
		}
		
		compileClasspath += project.sourceSets.main.compileClasspath
		compileClasspath += project.sourceSets.generatedVOs.output
		compileClasspath += configurations.generatedEntities
	}

	generatedXml {
		
		java {
			srcDirs = [ 'src-gen-xml' ]
		}
		resources {
			srcDirs = [ 'src-gen-xml' ]
		}
		
		compileClasspath += project.sourceSets.main.compileClasspath
		compileClasspath += project.sourceSets.generatedVOs.output
	}

	test {
		
		java {
			srcDirs = [ 'test', 'src-gen-entities', 'src-gen-vos', 'src-gen-xml' ]
		}
		resources {
			srcDirs = [ 'test', 'src-gen-entities', 'src-gen-vos', 'src-gen-xml' ]
		}
		
		compileClasspath += project.sourceSets.generatedEntities.output

		compileClasspath += project.sourceSets.generatedVOs.output
		
		compileClasspath += project.sourceSets.generatedXml.output
	}

}

task generatedEntitiesJar(type: Jar, dependsOn: compileGeneratedEntitiesJava) {
	classifier 'entities-generated'
	from sourceSets.generatedEntities.output
}

task generatedEntitiesSourceJar(type: Jar, dependsOn: compileGeneratedEntitiesJava) {
	classifier 'entities-generated-sources'
	from sourceSets.generatedEntities.allSource
	exclude('**/persistence.xml')
}

task generatedVOsJar(type: Jar, dependsOn: project.compileGeneratedVOsJava) {
	classifier 'vos-generated'
	from sourceSets.generatedVOs.output
}

task generatedVOsSourceJar(type: Jar, dependsOn: compileGeneratedVOsJava) {
	classifier 'vos-generated-sources'
	from sourceSets.generatedVOs.allSource
}

task generatedXmlJar(type: Jar, dependsOn: project.compileGeneratedXmlJava) {
	classifier 'xml-generated'
	from sourceSets.generatedXml.output
}

task generatedXmlSourceJar(type: Jar, dependsOn: compileGeneratedXmlJava) {
	classifier 'xml-generated-sources'
	from sourceSets.generatedXml.allSource
}

compileGeneratedEntitiesJava.dependsOn("xtextGenerate")
processGeneratedEntitiesResources.dependsOn("xtextGenerate")
compileGeneratedVOsJava.dependsOn("xtextGenerate")
processGeneratedVOsResources.dependsOn("xtextGenerate")
compileGeneratedXmlJava.dependsOn("xtextGenerate")
processGeneratedXmlResources.dependsOn("xtextGenerate")

artifacts {
	archives sourceJar, javadocJar
	generatedEntities generatedEntitiesJar, generatedEntitiesSourceJar
	generatedVOs generatedVOsJar, generatedVOsSourceJar
	generatedXml generatedXmlJar, generatedXmlSourceJar
}
