/*
 * generated by Xtext
 */
package io.pelle.mango.dsl.generator.server.service

import com.google.inject.Inject
import io.pelle.mango.dsl.generator.client.ClientNameUtils
import io.pelle.mango.dsl.generator.client.ClientTypeUtils
import io.pelle.mango.dsl.generator.client.web.BaseServices
import io.pelle.mango.dsl.generator.server.ServerNameUtils
import io.pelle.mango.dsl.generator.util.AttributeUtils
import io.pelle.mango.dsl.generator.util.ServiceUtils
import io.pelle.mango.dsl.mango.Service
import io.pelle.mango.dsl.mango.ServiceMethod

class RestServices {

	@Inject
	extension AttributeUtils

	@Inject
	extension ServerNameUtils server

	@Inject
	extension ServiceUtils

	@Inject
	extension BaseServices

	@Inject
	extension ClientTypeUtils

	ClientNameUtils clientNameUtils = new ClientNameUtils

	def restServiceControllerRequetVO(Service service, ServiceMethod method) '''
	package «service.packageName»;
	
	public class «restControllerRequestVOName(service, method)»  {
		«IF method.methodParameters.onlySimpleTypes»
			«FOR methodParamater : method.methodParameters»
				«attribute(getType(methodParamater), methodParamater.name)»
				«getter(getType(methodParamater), methodParamater.name.attributeName)»
				«setter(getType(methodParamater), methodParamater.name.attributeName)»
			«ENDFOR»
		«ENDIF»
	}
	'''

	def restServiceController(Service service) '''
		
		package «service.packageName»;
		
		import org.springframework.web.bind.annotation.RequestMapping;
		import org.springframework.web.bind.annotation.RequestMethod;
		import org.springframework.web.bind.annotation.RequestParam;
		import org.springframework.web.bind.annotation.RequestBody;
		import org.springframework.web.bind.annotation.PathVariable;
		import org.springframework.web.bind.annotation.ResponseBody;
		import org.springframework.transaction.annotation.Transactional;
		
		@SuppressWarnings("all")
		@org.springframework.web.bind.annotation.RestController
		@org.springframework.web.bind.annotation.RequestMapping("«service.restMapping»")
		public class «service.restControllerName»  {
		
			@org.springframework.beans.factory.annotation.Autowired
			private «clientNameUtils.serviceInterfaceFullQualifiedName(service)» «service.variableName»;
			
			public void «service.setterName»(«clientNameUtils.serviceInterfaceFullQualifiedName(service)» «service.variableName»)
			{
				this.«service.variableName» = «service.variableName»;
			}
			
			«FOR method : service.remoteMethods»
				«IF method.methodParameters.size == 1 && !method.methodParameters.onlySimpleTypes»
					@RequestMapping(value = "«method.restMapping»", method = RequestMethod.POST)
					@Transactional
					public «method.genericTypeDefinition.genericTypeDefinition» «method.serviceMethodReturnType» «method.name.toFirstLower»(@RequestBody «method.methodParameters.methodParameters») {
						«service.methodReturn(method)»
					}
				«ELSEIF method.methodParameters.onlySimpleTypes»
					@RequestMapping(value = "«method.restMapping»/«FOR parameter : method.methodParameters SEPARATOR "/"»{«parameter.name.toFirstLower»}«ENDFOR»", produces="application/json", method = RequestMethod.GET)
					@ResponseBody
					@Transactional
					public «method.genericTypeDefinition.genericTypeDefinition» «method.serviceMethodReturnType» «method.name.toFirstLower»Get(«FOR parameter : method.methodParameters SEPARATOR ", "»@PathVariable «parameter.type» «parameter.name.toFirstLower»«ENDFOR») {
						«service.methodReturn(method)»
					}

					@RequestMapping(value = "«method.restMapping»", produces="application/json", method = RequestMethod.POST)
					@ResponseBody
					@Transactional
					public «method.genericTypeDefinition.genericTypeDefinition» «method.serviceMethodReturnType» «method.name.toFirstLower»Post(«FOR parameter : method.methodParameters SEPARATOR ", "»@RequestParam «parameter.type» «parameter.name.toFirstLower»«ENDFOR») {
						«service.methodReturn(method)»
					}
					
					@RequestMapping(value = "«method.restMapping»", produces="application/json", method = RequestMethod.POST, consumes = "application/json")
					@ResponseBody
					@Transactional
					public «method.genericTypeDefinition.genericTypeDefinition» «method.serviceMethodReturnType» «method.name.toFirstLower»PostRequestBody(@RequestBody «restControllerRequestVOName(service, method)» requestBody) {
						«IF method.hasReturn»return«ENDIF» this.«service.variableName».«method.name.toFirstLower»(«FOR parameter : method.methodParameters SEPARATOR ","»requestBody.get«parameter.name.toFirstUpper»()«ENDFOR»);
					}
				«ENDIF»
			«ENDFOR»
		}
	'''

	def methodReturn(Service service, ServiceMethod method) '''
		«IF method.hasReturn»return«ENDIF» this.«service.variableName».«method.name.toFirstLower»(«FOR parameter : method.methodParameters SEPARATOR ","»«parameter.name.toFirstLower»«ENDFOR»);
	'''

}
